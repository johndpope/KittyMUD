%{
#import <stdio.h>
#import <string.h>
#import "KMXDFExpression.h"
#import "KMXDFReference.h"
	
void XDFerror(const char *str)
{
	fprintf(stderr,"error: %s\n",str);
}
	
int XDFwrap()
{
	return 1;
}
	
#define YYPARSE_PARAM parm
%}

%union {
	float num;
	NSString* text;
	KMXDFOpType op;
	id ref;
}

%token STATREFSTART STATREFEND LPAREN RPAREN FUNCSTART FUNCEND VARREFSTART VARREFEND
%token <op> OPADD OPSUBTRACT OPMULTIPLY OPDIVIDE OPPERCENT OPMODULUS
%token <num> NUMBER
%token <text> IDENTIFIER

%type <ref> simplereference statreference varreference funcreference groupexpression simpleopreference expression expressions simpleoperation
%type <op> unooperation duooperation

%left OPADD OPSUBTRACT
%left OPMULTIPLY OPDIVIDE OPMODULUS
%left OPPERCENT

%%
expressions: /* empty */ { *((id*)parm) = nil; } | expressions expression { *((id*)parm) = $2; } ;

expression: simpleoperation { $$ = $1; } | simplereference { $$ = $1; } | groupexpression { $$ = $1; } ;

groupexpression: LPAREN expression RPAREN {	$$ = $2; } ;

simpleoperation: simpleopreference duooperation simpleopreference
	{
		KMXDFExpression* expression = [[KMXDFExpression alloc] init];
		[expression setReference0:$1];
		[expression setReference1:$3];
		[expression setOperationType:$2];
		KMXDFReference* ref = [[KMXDFReference alloc] init];
		[ref setType:KMXDFExpressionRef];
		[ref setExpression:expression];
		$$ = ref;
	}
	| simpleopreference unooperation
	{
		KMXDFExpression* expression = [[KMXDFExpression alloc] init];
		[expression setReference0:$1];
		[expression setReference1:nil];
		[expression setOperationType:$2];
		KMXDFReference* ref = [[KMXDFReference alloc] init];
		[ref setType:KMXDFExpressionRef];
		[ref setExpression:expression];
		$$ = ref;	
	}
	;

duooperation: OPADD { $$ = $1; } | OPSUBTRACT {	$$ = $1; } | OPMULTIPLY { $$ = $1; } | OPDIVIDE { $$ = $1; } | OPMODULUS { $$ = $1; } ;

unooperation: OPPERCENT { $$ = $1; } ;

simpleopreference: groupexpression { $$ = $1; } | simplereference { $$ = $1; } ;

simplereference: NUMBER
	{
		KMXDFReference* ref = [[KMXDFReference alloc] init];
		[ref setType:KMXDFNumberRef];
		[ref setNumber:$1];
		$$ = ref;
	}
	| statreference { $$ = $1; }
	| varreference { $$ = $1; }
	| funcreference { $$ = $1; }
	;

statreference: STATREFSTART IDENTIFIER STATREFEND
	{
		KMXDFReference* ref = [[KMXDFReference alloc] init];
		[ref setType:KMXDFStatRef];
		[ref setReference:$2];
		$$ = ref;
	}
	;

varreference: VARREFSTART IDENTIFIER VARREFEND
	{
		KMXDFReference* ref = [[KMXDFReference alloc] init];
		[ref setType:KMXDFVarRef];
		[ref setReference:$2];
		$$ = ref;
	}
	;

funcreference: FUNCSTART IDENTIFIER LPAREN expression RPAREN FUNCEND
	{
		KMXDFReference* ref = [[KMXDFReference alloc] init];
		[ref setType:KMXDFFuncRef];
		[ref setReference:$2];
		[ref setExpression:$4];
		$$ = ref;
	}
	;